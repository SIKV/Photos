plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'com.google.gms.google-services'
    id 'com.google.android.gms.oss-licenses-plugin'
}

def releaseKeyAlias = hasProperty('releaseKeyAlias') ? property('releaseKeyAlias') : null
def releaseKeyPassword = hasProperty('releaseKeyPassword') ? property('releaseKeyPassword') : null
def releaseStoreFile = hasProperty('releaseStoreFile') ? property('releaseStoreFile') : null
def releaseStorePassword = hasProperty('releaseStorePassword') ? property('releaseStorePassword') : null

android {
    defaultConfig {
        applicationId 'com.github.sikv.photos'
        versionCode 1
        versionName '1.0'
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    signingConfigs {
        release {
            keyAlias releaseKeyAlias
            keyPassword releaseKeyPassword
            storeFile releaseStoreFile ? file(releaseStoreFile) : null
            storePassword releaseStorePassword
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            ndk {
                debugSymbolLevel = 'FULL'
            }
            signingConfig signingConfigs.release
        }
        releaseDebuggable {
            initWith buildTypes.release
            matchingFallbacks = ['release']
            debuggable true
        }
        benchmark {
            initWith buildTypes.release
            signingConfig signingConfigs.debug
            matchingFallbacks = ['release']
        }
    }

    with flavorConfig

    buildFeatures {
        viewBinding true
    }

    lintOptions {
        checkAllWarnings true
        warningsAsErrors true
        abortOnError true
        baseline file('lint-baseline.xml')

        disable 'InvalidPackage'
    }
}

kapt {
    generateStubs = false
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation project(':api')
    implementation project(':domain')
    implementation project(":config")
    implementation project(":data")
    implementation project(":common")
    implementation project(":common-ui")
    implementation project(':navigation')
    implementation project(':theme-manager')
    implementation project(":photo-list-ui")
    implementation project(":feature:curated-photos")
    implementation project(":feature:photo-details")
    implementation project(":feature:wallpaper")
    implementation project(":feature:search")
    implementation project(":feature:favorites")
    implementation project(":feature:recommendations")
    implementation project(":feature:preferences")
    implementation project(":feature:feedback")

    implementation deps.appcompat
    implementation deps.material
    implementation deps.viewpager2

    implementation deps.hilt_android
    kapt deps.hilt_compiler

    // https://stackoverflow.com/a/60492942/7064179
    implementation 'com.google.guava:listenablefuture:9999.0-empty-to-avoid-conflict-with-guava'

    testImplementation 'junit:junit:4.13.2'

    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'androidx.arch.core:core-testing:2.1.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'com.linkedin.dexmaker:dexmaker-mockito:2.28.3'
}
