buildscript {
    ext.kotlin_version = '1.7.0'

    ext.flavorConfig = {
        flavorDimensions 'version'

        productFlavors {
            dev {
                dimension 'version'
                applicationIdSuffix '.dev'
                versionNameSuffix '-dev'
            }
            prod {
                dimension 'version'
            }
        }
    }

    ext.libFlavorConfig = {
        flavorDimensions 'version'

        productFlavors {
            dev {
                dimension 'version'
            }
            prod {
                dimension 'version'
            }
        }
    }

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.0.4'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.google.dagger:hilt-android-gradle-plugin:2.42'
        classpath 'com.google.gms:google-services:4.3.13'
        classpath 'com.google.android.gms:oss-licenses-plugin:0.10.5'
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

subprojects {
    afterEvaluate { project ->
        if (project.hasProperty('android')) {
            android {
                compileSdk 32

                defaultConfig {
                    minSdk 23
                    targetSdk 32
                }

                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_1_8
                    targetCompatibility JavaVersion.VERSION_1_8
                }

                kotlinOptions {
                    jvmTarget = '1.8'
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

apply from: 'deps.gradle'
